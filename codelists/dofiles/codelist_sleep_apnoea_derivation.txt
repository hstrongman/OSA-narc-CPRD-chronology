<<dd_version: 2>>
<<dd_include: header.txt>>

Description of how the sleep apnoea code lists were created
===============================================================

~~~~
This HTML file includes the information needed for clinicians
and researchers to understand how the code lists are derived.
Researchers can update the code lists following the instructions in
codelist_sleep_apnoea.do.

<<dd_do:quietly>>
use "`2'", clear

/*The following commands create common variables for the three datasets.
Variables preceded by underscore are temporary and are deleted at the
end of the script. The commands and outputs do not appear in the
HTML file because the attribute "quietly" is used above.*/

if "`1'" == "aurum" {
	destring observations, replace
	rename cleansedreadcode readcode
	}
if "`1'" == "gold" {
	egen observations = rowtotal(clinevents refevents testevents imevents)
	rename readterm term
	}
if "`1'" == "hesapc" {
	gen observations = .
	rename description term
	rename code ICD /*ICD matches HES APC data dictionary*/
	}
	
<</dd_do>>
~~~~
## BASIC SEARCH
~~~~

<<dd_do:quietly>>
gen _term_lower = lower(term)
generate _marker=.
/*these inclusion and exclusion terms are displayed and searched for below. 
Changes should be made here. These types of searches are common to most code
lists*/
local interms "apnoea apnea OSA"
local inchaps "H5B R005 Fy03"
local exterms "mosaic mask questionnaire clinic appliance treatment"
local exterms "`exterms' monitoring recording assessment operation"
local exchaps ""
<</dd_do>>

The following search terms are used to generate the initial code lists. Each 
term is searched for individually and included or excluded if 
it appears in any position in the text. The terms should cover all synonyms
for sleep apnoea. Chapter searches only apply to code lists that include
Read codes; these include CPRD Aurum code lists as a large proportion of Snomed
codes are matched to Read.

<<dd_do: nocommands>>
display "Inclusion terms: `interms'"
display "Inclusion chapters: `inchaps'"
if "`1'" == "gold" | "`1'" == "aurum" display "Exclusion terms: `exterms'"
display "Exclusion chapters: `exchaps'"
<</dd_do>>

Note: Care pathway terms are excluded in the new code list because they are 
rarely used and mostly indicate testing for sleep apnoea

<<dd_do:quietly>>
foreach word of local interms {
	replace _marker = 1 if strmatch(term, "*`word'*")
	replace _marker = 1 if strmatch(_term_lower, "*`word'*")
	}

if "`1'" == "gold" | "`1'" == "aurum" {
	foreach chap of local inchaps {
		replace _marker = 1 if strmatch(readcode, "`chap'*")
		}
	}

keep if _marker == 1

foreach word of local exterms {
	replace _marker = 0 if strmatch(_term_lower, "*`word'*")
	}

<</dd_do>> 

~~~~
## CODE LIST SPECIFIC SEARCH ALGORITHM
~~~~

Terms without both "sleep" and either "apnoea" or "apnea" are
excluded.

<<dd_do:quietly>>
replace _marker = 0 if !strmatch(_term_lower, "*sleep*")
replace _marker = 0 if !strmatch(_term_lower, "*apnoea*") & !strmatch(_term_lower, "*apnea*")
<</dd_do>>

~~~~
## CHECK EXCLUDED CODES
~~~~

The following list includes all codes that have been excluded from the code
list. This should be checked carefully.

<<dd_do: nocommands>>
list `3' `4' term if _marker == 0, noobs table
<</dd_do>>

~~~~
## CREATE CATEGORIES
~~~~

<<dd_do:quietly>>
drop if _marker == 0
gen category = 0
label define categorylab 1 "Obstructive Sleep Apnoea" ///
2"Obstructive sleep apnoea syndrome" ///
3 "Sleep apnoea NOS" ///
4 "Sleep apnoea syndrome NOS" ///
5 "Central sleep apnoea" ///
6 "Primary sleep apnoea"
label values category categorylab
/*each category is defined separately below. This approach might need to be
adapted if new codes are added to the dictionaries.*/
<</dd_do>>

The categories are defined using the following criteria. They are not in
numeric order because the categories that are defined later use information
used to create earlier categories. These are provided so that the search can
be replicated using different software.

1: Obstructive Sleep Apnoea
This category includes terms including the strings "obstructive" or "mixed" but not "syndrome"

<<dd_do:quietly>>
replace category = 1 if strmatch(_term_lower, "*obstructive*") & !strmatch(_term_lower, "*syndrome*")
replace category = 1 if strmatch(_term_lower, "*mixed*") & !strmatch(_term_lower, "*syndrome*")
<</dd_do>>

2: Obstructive Sleep Apnoea Syndrome
This category includes terms including "obstructive" or "mixed" and "syndrome"

<<dd_do:quietly>>
replace category = 2 if strmatch(_term_lower, "*obstructive*") & strmatch(_term_lower, "*syndrome*")
replace category = 2 if strmatch(_term_lower, "*mixed*") & strmatch(_term_lower, "*syndrome*")
replace category = 1 if category == 2 & strmatch(_term_lower, "*dysmorphism syndrome*")
<</dd_do>>

5. Central sleep apnoea
This category includes terms including the string "central"

<<dd_do:quietly>>
local specterms = "central"
foreach word of local specterms {
	replace category = 5 if strmatch(_term_lower, "*`word'*")
}
<</dd_do>>

6. Primary sleep apnoea
This category includes terms including the string "primary"

<<dd_do:quietly>>
local specterms = "primary"
foreach word of local specterms {
	replace category = 6 if strmatch(_term_lower, "*`word'*")
}
<</dd_do>>

4. Sleep apnoea syndrome NOS
This category includes terms that have not been classified and include the string "syndrome"

<<dd_do:quietly>>
replace category = 4 if category == 0 & strmatch(_term_lower, "*syndrome*")
<</dd_do>>

3. sleep apnoea NOS
This category includes terms that have not been classified

<<dd_do:quietly>>
replace category = 3 if category == 0
<</dd_do>>
~~~~
## CHECK INCLUDED CODES AND CATEGORISATION
~~~~

<<dd_do:quietly>>
gsort category -observations
<</dd_do>>

~~~~
## Codes by categorylab
~~~~

The following table includes all included terms and the assigned category.
It is provided so that researchers and clinicians can check whether all codes
are relevant and categorised correctly.

<<dd_do: nocommands>>
list `3' `4' category term observations, noobs sepby(category)
<</dd_do>>


~~~~
## Number of observations per category
~~~~

The following table described the number of observations in the CPRD database
for each category. This information can be used to sense check the proposed
phenotype definitions in codelist_sleep_apnoea_description.html.

Observations counts will be O for HES APC data because there is no equivalent
field in the dictionary.

<<dd_do: quietly>>
save "_temp.dta", replace /*can't use preserve/restore*/
quietly collapse (sum) observations, by(category)
<</dd_do>>

<<dd_do: nocommands>>
list
<</dd_do>>

<<dd_do: quietly>>
list
use "_temp.dta", clear
<</dd_do>>


~~~~
## Compare with previous version
~~~~

<<dd_do: quietly>>
/*Code in this section compares the new code list with a previous version.
It accounts for changes in category coding between the old and new code list
(i.e. other specified changed to separate central and primary sleep apnoea
categories).
It could be adapted to compare multiple code lists from other sources.*/
save "_temp.dta", replace
rename category _categorynew
label variable _categorynew "Category in new codelist"

local source = "`1'"
merge 1:1 `3' using "$olddir\cr_codelist_apnoea_`source'"
gen _comparison = _merge

label define _comparisonlab 1 "New only" 2 "Old only" 3 "Both"
label values _comparison _comparisonlab
label variable _comparison "Comparison of old and updated code list"

rename category _categoryold
label variable _categoryold "Category in old codelist"
label copy categorylab categorylabold
label define categorylabold 5 "Sleep apnoea (other specified)" 6 "Care pathway", modify 
label values _categoryold categorylabold

gsort _comparison _categorynew _categoryold -observations
<</dd_do>>

The following information compares the new code list with the previous version.

<<dd_do: nocommands>>
tab _comparison
qui count if _comparison == 1
<</dd_do>>

<<dd_if: `r(N)' > 0>>
The following codes are in the new code list but are not in the old code list:
<<dd_else>>
All the codes in the new code list were in the old code list.
<<dd_endif>>

<<dd_do: nocommands>>
list term _categorynew _categoryold observations if _comparison == 1, noobs table
<</dd_do>>

<<dd_do: quietly>>
count if _comparison == 2
<</dd_do>>

<<dd_if: `r(N)' > 0>>
The following codes are in the old code list but are not in the new code list:
<<dd_else>>
All the codes in the old code list are in the new code list.
<<dd_endif>>

<<dd_do: nocommands>>
list term _categorynew _categoryold observations if _comparison == 2, noobs table
<</dd_do>>

<<dd_do: quietly>>
count if _comparison == 3 & _categorynew != _categoryold
<</dd_do>>

<<dd_if: `r(N)' > 0>>
The following codes are in both code lists but in a different category:
<<dd_else>>
All codes that are in both code lists are in the same category.
<<dd_endif>>

<<dd_do: nocommands>>
list term _categorynew _categoryold observations if _comparison == 3 & _categorynew != _categoryold, noobs table
<</dd_do>>


<<dd_do: quietly>>
use "_temp.dta", clear
drop _*
save "_temp.dta", replace
if "`1'" != "aurum" drop observations
export delimited using "$datadir_text\codelist_sleep_apnoea_`1'.txt", replace delimiter(tab)
use "_temp.dta", clear
keep `3' `4' term observations category
save "$datadir_stata\codelist_sleep_apnoea_`1'.dta", replace
erase "_temp.dta"
<</dd_do>>


